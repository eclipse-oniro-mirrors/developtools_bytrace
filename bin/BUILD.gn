# Copyright (C) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//developtools/bytrace/bytrace.gni")

config("bytrace_osal_inner_config") {
  visibility = [ ":*" ]
  include_dirs = [
    "./include",
    "//base/hiviewdfx/hitrace/interfaces/native/innerkits/include/hitrace_meter",
  ]
}

ohos_static_library("bytrace_osal_inner") {
  sources = [ "./src/bytrace_osal.cpp" ]
  public_configs = [ ":bytrace_osal_inner_config" ]
  external_deps = [
    "init:libbegetutil",
    "ipc:ipc_core",
  ]

  subsystem_name = "developtools"
  part_name = "bytrace"
}

ohos_executable("bytrace") {
  install_enable = true
  sources = [ "./src/bytrace_cmd.cpp" ]

  deps = [
    ":bytrace_osal_inner",
    "//third_party/zlib:libz",
    "//utils/native/base:utils",
  ]

  external_deps = [ "hitrace_native:hitrace_meter" ]

  include_dirs = [
    "${bytrace_path}/bin/include",
    "//base/hiviewdfx/hitrace/interfaces/native/innerkits/include/hitrace_meter",
    "//utils/native/base/include",
    "//third_party/zlib",
  ]
  subsystem_name = "developtools"
  part_name = "bytrace"
}

ohos_prebuilt_etc("bytrace.cfg") {
  source = "./config/bytrace.cfg"
  relative_install_dir = "init"
  subsystem_name = "developtools"
  part_name = "bytrace"
}

ohos_executable("bytrace_example") {
  sources = [ "example/bytrace_example.cpp" ]
  external_deps = [
    "hitrace_native:hitrace_meter",
    "hiviewdfx_hilog_native:libhilog",
  ]
  subsystem_name = "developtools"
  part_name = "bytrace"
}

group("bytrace_target") {
  deps = [
    ":bytrace",
    ":bytrace_example",
  ]
}
